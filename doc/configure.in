dnl -*- sh -*-
dnl
dnl  Authors:
dnl    Denys Duchier (duchier@ps.uni-sb.de)
dnl 
dnl  Copyright:
dnl    Denys Duchier (1998)
dnl 
dnl  Last change:
dnl    $Date$ by $Author$
dnl    $Revision$
dnl 
dnl  This file is part of Mozart, an implementation 
dnl  of Oz 3:
dnl     http://www.mozart-oz.org
dnl 
dnl  See the file "LICENSE" or
dnl     http://www.mozart-oz.org/LICENSE.html
dnl  for information on usage and redistribution 
dnl  of this file, and for a DISCLAIMER OF ALL 
dnl  WARRANTIES.
dnl

dnl Process this file with autoconf to produce a configure script.

AC_INIT(ozdoc.dtd)
OZ_INIT
OZ_PATH_PROG(OZENGINE,ozengine)
OZ_PATH_PROG(OZDOC,ozdoc,OZDOC=$BUILDTOP/doc/utilities/ozdoc)
AC_SUBST(OZENGINE)
AC_SUBST(OZDOC)

AC_MSG_CHECKING(for --with-java-bin)
AC_ARG_WITH(java-bin,
  [--with-java-bin='DIR'
       directory where java executables/wrappers reside],
  oz_java_bin=${with_java_bin})
: ${oz_java_bin=}
AC_MSG_RESULT($oz_java_bin)
if test -n "${oz_java_bin}" ;
then
  if expr "${oz_java_bin}" : '.*/$' ;
  then
    # really do nothing portably
    oz_tmp=
  else
    oz_java_bin=${oz_java_bin}/
  fi
fi
JAVA_BIN=${oz_java_bin}
AC_SUBST(JAVA_BIN)

AC_MSG_CHECKING(for --with-java-threads)
AC_ARG_WITH(java-threads,
  [--with-java-threads='OPTION'
       option for java threads support selection (default=-green)],
  oz_java_threads=${with_java_threads})
: ${oz_java_threads=-green}
AC_MSG_RESULT($oz_java_threads)
JAVA_THREADS=${oz_java_threads}
AC_SUBST(JAVA_THREADS)

AC_MSG_CHECKING(for --with-documents)
AC_ARG_WITH(documents,
  [--with-documents='DIR1 ... DIRN' (or all)      
       document subdirectories (default=none)],
  oz_doc_dirs=${with_documents})
: ${oz_doc_dirs=}
AC_MSG_RESULT($oz_doc_dirs)

test "${oz_doc_dirs}" = "all" && oz_doc_dirs="apptut base bugs compiler contrib demo foreign gump op opi ozcar profiler ozdoc system tools wp fdt fst cpitut cpiref install tutorial overview panel explorer browser notation dstutorial changes tcltk howtoread faq limitations macro loop qtk dpanel"

DOC_DIRS=$oz_doc_dirs
AC_SUBST(DOC_DIRS)

AC_MSG_CHECKING(for --enable-chm)
AC_ARG_ENABLE(chm,
  [--enable-chm
       construct CHM table of contents files (default=no)],
  oz_enable_chm=$enable_chm,
  oz_enable_chm=no)
AC_MSG_RESULT($oz_enable_chm)

if test "${oz_enable_chm}" != no
then
   SUBDIRS="utilities ${oz_doc_dirs} chm"
else
   SUBDIRS="utilities ${oz_doc_dirs}"
fi

OZ_ENABLE(qtk,[QTk QUI Builder],no,QTK_ENABLED=yes,QTK_ENABLED=no)
AC_SUBST(QTK_ENABLED)

if test "$QTK_ENABLED" = yes;
then
  SUBDIRS="$SUBDIRS qtk"
fi

AC_SUBST(SUBDIRS)

AC_CONFIG_SUBDIRS(utilities chm)

AC_OUTPUT(Makefile Makefile.common Makefile.vars catalog bootcatalog base/Makefile bugs/Makefile foreign/Makefile apptut/Makefile wp/Makefile system/Makefile compiler/Makefile opi/Makefile tools/Makefile ozcar/Makefile profiler/Makefile gump/Makefile ozdoc/Makefile contrib/Makefile demo/Makefile op/Makefile fdt/Makefile fst/Makefile cpitut/Makefile cpiref/Makefile install/Makefile tutorial/Makefile overview/Makefile notation/Makefile browser/Makefile panel/Makefile explorer/Makefile dstutorial/Makefile changes/Makefile tcltk/Makefile howtoread/Makefile faq/Makefile limitations/Makefile xsl/Makefile xsl/ozdoc-info.sty:xsl/ozdoc-info.in macro/Makefile loop/Makefile qtk/Makefile dpanel/Makefile qtk/Makefile)

errprint(ifdef([OZ_INIT],
 [autoconf succeeded
],
 [autoconf failed: use 'autoconf -l ..' to include ../aclocal.m4.
]))
