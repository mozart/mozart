dnl -*- sh -*-
dnl
dnl  Authors:
dnl    Michael Mehl (mehl@dfki.de)
dnl
dnl  Contributors:
dnl    optional, Contributor's name (Contributor's email address)
dnl
dnl  Copyright:
dnl    Michael Mehl (1997)
dnl
dnl  Last change:
dnl    $Date$ by $Author$
dnl    $Revision$
dnl
dnl  This file is part of Mozart, an implementation
dnl  of Oz 3:
dnl     $MOZARTURL$
dnl
dnl  See the file "LICENSE" or
dnl     $LICENSEURL$
dnl  for information on usage and redistribution
dnl  of this file, and for a DISCLAIMER OF ALL
dnl  WARRANTIES.
dnl

dnl Process this file with autoconf to produce a configure script.

dnl *****************************************************************
dnl INITIALIZE
dnl *****************************************************************

dnl This will check that we are in the Oz directory and initiate
AC_INIT(ozMain.c)

OZ_INIT

AC_PROG_CC

AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_CHECK_PROGS(RM, rm, rm)
AC_CHECK_PROGS(MV, mv, mv)
AC_CHECK_PROGS(ECHO, echo, echo)

OZ_PATH_PROG(PLATFORMSCRIPT, ozplatform)
OZ_PATH_PROG(MKINSTALLDIRS, mkinstalldirs)

dnl *****************************************************************
dnl platform
dnl *****************************************************************

: ${platform=`$PLATFORMSCRIPT`}
AC_SUBST(platform)

: ${PROGRAMNAME=oz.emulator.bin}
TARGETS="$PROGRAMNAME"
AC_SUBST(PROGRAMNAME)
AC_SUBST(TARGETS)

PLATFORM="`echo $platform|sed -e s/-/_/|tr a-z A-Z`"
AC_DEFINE_UNQUOTED($PLATFORM)
AC_SUBST(PLATFORM)

dnl *****************************************************************
dnl Checks for libraries.
dnl *****************************************************************

AC_CHECK_LIB(nsl, gethostbyaddr)
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(m, fabs)
AC_CHECK_LIB(dl, dlopen)

dnl *****************************************************************
dnl *** X11
dnl *****************************************************************

AC_MSG_CHECKING(for --with-x11)
AC_ARG_WITH(x11,
    [--with-x11=<dir>
        search x11 library and header in <dir> (default: NONE)])

if test -n "${with_x11}"
then
    oz_x11_lib_dir=$with_x11
    oz_x11_inc_dir=$with_x11
fi

AC_ARG_WITH(x11_lib,
    [--with-x11-lib=<dir>
        search x11 library in <dir> (default: /usr/local/lib)])

if test -n "${with_x11_lib}"
then
    oz_x11_lib_dir=$with_x11_lib
fi

AC_ARG_WITH(x11_include,
    [--with-x11-include=<dir>
        search x11 header in <dir> (default: /usr/local/include)])

if test -n "${with_x11_include}"
then
    oz_x11_inc_dir=$with_x11_include
fi

: ${oz_x11_lib_dir=/usr/local/lib}
: ${oz_x11_inc_dir=/usr/local/include}

AC_MSG_RESULT(trying \"-L$oz_x11_lib_dir -I$oz_x11_inc_dir\")

CPPFLAGS="-I$oz_x11_inc_dir $CPPFLAGS"
AC_CHECK_HEADER(X11/Xlib.h,,
        AC_MSG_ERROR(required X include files not found))

LDFLAGS="-L$oz_x11_lib_dir $LDFLAGS"
AC_CHECK_LIB(X11, XOpenDisplay, ,
        AC_MSG_ERROR(required lib "-lX11" not found))

dnl *****************************************************************
dnl *** Tcl/Tk
dnl *****************************************************************

AC_MSG_CHECKING(for --with-tk)
AC_ARG_WITH(tk,
    [--with-tk=<dir>
        search tk library and header in <dir> (default: NONE)])

if test -n "${with_tk}"
then
    oz_tk_lib_dir=$with_tk
    oz_tk_inc_dir=$with_tk
fi

AC_ARG_WITH(tk_lib,
    [--with-tk-lib=<dir>
        search tk library in <dir> (default: /usr/local/lib)])

if test -n "${with_tk_lib}"
then
    oz_tk_lib_dir=$with_tk_lib
fi

AC_ARG_WITH(tk_include,
    [--with-tk-include=<dir>
        search tk header in <dir> (default: /usr/local/include)])

if test -n "${with_tk_include}"
then
    oz_tk_inc_dir=$with_tk_include
fi

: ${oz_tk_lib_dir=/usr/local/lib}
: ${oz_tk_inc_dir=/usr/local/include}

AC_MSG_RESULT(trying \"-L$oz_tk_lib_dir -I$oz_tk_inc_dir\")

CPPFLAGS="-I$oz_tk_inc_dir $CPPFLAGS"
AC_CHECK_HEADER(tcl.h,,
        AC_MSG_ERROR(required tcl.h include file not found))
AC_CHECK_HEADER(tk.h,,
        AC_MSG_ERROR(required tk.h include file not found))

LDFLAGS="-L$oz_tk_lib_dir $LDFLAGS"
AC_CHECK_LIB(tcl, Tcl_Init, ,
        AC_MSG_ERROR(required lib "-ltcl" not found))

AC_CHECK_LIB(tk, Tk_Init, ,
        AC_MSG_ERROR(required lib "-ltk" not found))


dnl *****************************************************************
dnl special hack for static linking under linux
dnl *****************************************************************

case $platform in
    linux*)
        LIBS=`echo $LIBS | sed -e 's/[ ]-ldl//g'`
        LIBS="-ldl -Xlinker -Bstatic $LIBS"
        ;;
esac

dnl *****************************************************************
dnl FINALIZE
dnl *****************************************************************

AC_OUTPUT(Makefile)

errprint(ifdef([OZ_INIT],
 [],
 [autoconf failed: use 'autoconf -l ..' to include ../aclocal.m4.
]))
